import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'site.equipable'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "essentialsx-releases"
        url = "https://repo.essentialsx.net/releases/"
    }

    // Paper
    maven { url "https://repo.papermc.io/repository/maven-public/" }

    // Skript
    maven {
        url "https://repo.skriptlang.org/releases"
    }
}

shadowJar {

    archiveFileName = project.name + "-" + project.version + ".jar"
    relocate("org.bstats", "site.equipable.skEssentials.metrics")
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT")
    compileOnly("com.github.SkriptLang:Skript:2.7.0")
    compileOnly("net.essentialsx:EssentialsX:2.20.1")
    implementation("org.bstats:bstats-bukkit:3.0.2")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

build {
    dependsOn(shadowJar)
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filter ReplaceTokens, tokens: ["version": project.version]
}
